glob int startDate
glob int endDate
glob int fundCloseDate
glob int goal
glob address receiver

glob mut int donated_total
loc mut int donated_local

Create crowdfund(int startDate, int endDate, int fundCloseDate, int goal, address receiver) {
	glob.startDate = startDate
	glob.endDate = endDate
	glob.fundCloseDate = fundCloseDate
	glob.goal = goal
	glob.receiver = receiver
	glob.donated_total = 0
	loc.donated_local = 0
}

OptIn optin() {
	loc.donated_local = 0
}

@timestamp (glob.startDate, glob.endDate)
@pay $donated ALGO : caller -> escrow
donate() {
	glob.donated_total += donated
	loc.donated_local += donated
}

@assert glob.donated_total < glob.goal
@timestamp (glob.endDate, )
@pay (, loc.donated_local)$reclaimed ALGO : escrow -> caller
reclaim() {
	loc.donated_local -= reclaimed
}
		
@assert glob.donated_total >= glob.goal
@timestamp (glob.endDate, )
@pay glob.donated_total : escrow -> glob.receiver
claim() {
	glob.donated_total = 0
}

@assert glob.donated_total == 0
@timestamp (glob.fundCloseDate, )
@close : escrow -> creator
@from creator
Delete delete() {}