glob int startDate
glob int endDate
glob int fundCloseDate
glob int goal
glob address receiver 
glob int donated_total
loc int escrow_local

Create create(int startDate, int endDate, int fundCloseDate, int goal, address receiver) {
	glob.startDate = startDate
	glob.endDate = endDate
	glob.fundCloseDate = fundCloseDate
	glob.goal = goal
	glob.receiver = receiver
	glob.donated_total = 0
}

OptIn optin() {
	loc.escrow_local = 0
}

@timestamp (glob.startDate, glob.endDate)
@pay $amount : call.sender -> escrow
NoOp donate() {
	glob.donated_total += amount
	loc.escrow_local += amount
}

@assert glob.donated_total < glob.goal
@timestamp (glob.endDate, )
@pay (, escrow_local)$amount ALGO : escrow -> call.sender	
NoOp reclaim() {
	loc.escrow_local -= amount
}

@assert glob.donated_total >= glob.goal 
@timestamp (glob.endDate, ) 
@pay glob.donated_total : escrow -> glob.receiver
NoOp claim() {} 

@assert glob.donated_total == 0
@timestamp (glob.fundCloseDate, )
@close escrow -> global.creator
@from global.creator
Delete delete() {}