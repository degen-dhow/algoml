glob int hands_to_play
glob int turn_started_at
glob address player1
glob address player2
glob int score1
glob int score2
glob string move_commit1
glob string move_commit2
glob string move1
glob string move2
glob int guess1
glob int guess2

/******************************
 Contract creation
 ******************************/

@gstate -> joined0
@round $r
@assert hands_to_play > 0
Create morra(int hands_to_play) {
    glob.hands_to_play = hands_to_play
    glob.turn_started_at = r
    glob.score1 = 0
    glob.score2 = 0
}

/******************************
 Joining the game
 ******************************/

@gstate joined0 -> joined1
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@pay 1 of ALGO : caller -> escrow
join() {
    glob.player1 = caller
    glob.turn_started_at = r
}

@gstate joined1 -> hand1
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@pay 1 of ALGO : caller -> escrow
join() {
    glob.player2 = caller
    glob.turn_started_at = r
}

@gstate joined1 -> end
@round (glob.turn_started_at+200,)
@close ALGO : escrow -> glob.player1
redeem() {}


/******************************
 Playing a hand
 ******************************/

@gstate hand1 -> hand2
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert glob.hands_to_play > 0
move(string move_commit, int guess) {
    glob.hands_to_play = glob.hands_to_play - 1
    glob.move_commit1 = move_commit
    glob.guess1 = guess
    glob.turn_started_at = r
}

@gstate hand2 -> reveal1
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert glob.move_commit1 != move_commit
move(string move_commit, int guess) {
    glob.move_commit2 = move_commit
    glob.guess2 = guess
    glob.turn_started_at = r
}


/******************************
 Opening the commitments
 ******************************/

// player1 must reveal the move first
@gstate reveal1 -> reveal2
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert sha256(move) == glob.move_commit1
reveal(string move) {
    glob.move1 = move
    glob.turn_started_at = r
}

// player2 must reveal the move after player1
@gstate reveal2 -> winhand
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert sha256(move) == glob.move_commit2
reveal(string move) {
    glob.move2 = move
    glob.turn_started_at = r
}


/******************************
 Scoring the hand
 ******************************/

// player1 does not commit in time
@gstate hand1 -> hand1
@round (glob.turn_started_at+100,)$r
newhand() {
    glob.score2 = glob.score2 + 1
    glob.turn_started_at = r
}

// player2 does not commit in time
@gstate hand2 -> hand1
@round (glob.turn_started_at+100,)$r
newhand() {
    glob.score1 = glob.score1 + 1
    glob.turn_started_at = r
}

// player1 does not reveal in time
@gstate reveal1 -> hand1
@round (glob.turn_started_at+100,)$r
newhand() {
    glob.score1 = glob.score1 + 1
    glob.turn_started_at = r
}

// if player2 has not revealed, player1 can redeem the pot
@gstate reveal2 -> hand1
@round (glob.turn_started_at+100,)$r
newhand() {
    glob.score1 = glob.score1 + 1
    glob.turn_started_at = r
}

// player1 wins the hand
@gstate winhand -> hand1
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert glob.guess1 == len(glob.move1) + len(glob.move2)
newhand() {
    glob.turn_started_at = r
    glob.score1 = glob.score1 + 1
}

// player2 wins the hand
@gstate winhand -> hand1
@round (glob.turn_started_at,glob.turn_started_at+100)$r
@assert glob.guess2 == len(glob.move1) + len(glob.move2)
newhand() {
    glob.score2 = glob.score2 + 1
    glob.turn_started_at = r
}

// no one wins the hand, or no one claims the point
@gstate winhand -> hand1
@round (glob.turn_started_at+100)$r
newhand() {
    glob.turn_started_at = r
}

/******************************
 Closing the game
 ******************************/

@gstate hand1 -> end
@assert glob.hands_to_play == 0
@close ALGO : escrow -> glob.player1 
@assert glob.score1 > glob.score2
win() {}

@gstate hand1 -> end
@assert glob.hands_to_play == 0
@close ALGO : escrow -> glob.player2
@assert glob.score2 > glob.score1
win() {}


/******************************
 Deleting the contract
 ******************************/

// delete a termibated contract
@gstate end -> 
@from creator
Delete delete() {}
